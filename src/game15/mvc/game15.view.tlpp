/*
                                _  ____             _
  __ _   __ _  _ __ ___    ___ / || ___|    __   __(_)  ___ __      __
 / _` | / _` || '_ ` _ \  / _ \| ||___ \    \ \ / /| | / _ \\ \ /\ / /
| (_| || (_| || | | | | ||  __/| | ___) | _  \ V / | ||  __/ \ V  V /
 \__, | \__,_||_| |_| |_| \___||_||____/ (_)  \_/  |_| \___|  \_/\_/
 |___/

*/
#include "totvs.ch"
#include "game15.th"
#include "parmtype.ch"
#include "tlpp-core.th"

namespace game15
using namespace game15

class Game15View

    private data bInit as codeblock
    private data bValid as codeblock

    private data lActive as logical
    private data lExecute as logical

    public data aNotPlayButtonsActions as array

    public data oDlg as object
    public data oController as object
    public data oTPaintPanel as object

    public method New(oController as object) as object
    public method Activate() as logical
    public method DeActivate() as logical

    public method IsActive() as logical
    public method CanExecute() as logical

    public method SaveTopTable() as variant
    public method ShowTopTable() as variant
    public method ClearTopTable() as logical
    public method MsgAbout() as variant

    private method Create() as variant

endclass

method New(oController) class Game15View
    paramtype 1 var oController as object
    self:lActive:=.F.
    if (self:CanExecute().and.(GetClassName(oController)=="GAME15.GAME15CONTROLLER"))
        self:aNotPlayButtonsActions:=Array(0)
        self:oController:=oController
    endif
return(self)

method Activate() class Game15View
    if (;
            ((ValType(self:oController)=="O").and.(GetClassName(self:oController)=="GAME15.GAME15CONTROLLER"));
            .and.;
            (self:oController:IsActive());
            .and.;
            (!self:lActive).and.(self:CanExecute());
    )
        self:Create()
        self:lActive:=.T.
        self:oDlg:Activate(nil,nil,nil,.T.,self:bValid,nil,self:bInit,nil,nil)
    endif
return(self:lActive)

method DeActivate() class Game15View
    if (self:lActive)
        if (valType(self:oDlg)=="O")
            self:oDlg:End()
        endif
        FreeObj(@self:oTPaintPanel)
        FreeObj(@self:oDlg)
        FWFreeArray(@self:aNotPlayButtonsActions)
        DelClassIntf()
        self:lActive:=.F.
    endif
return(self:lActive)

method Create() class Game15View

    local bRestoreGameUpdateView as codeblock

    local cIcon as character
    local cTitle:=GAME15_TITLE as character

    local jCoordinates as json

    local oView:=self as object
    local oController:=self:oController as object

    begin sequence

        if (!oView:CanExecute())
            break
        endif

        bRestoreGameUpdateView:={|nID as numeric,nLeft as numeric,nTop as numeric|oView:oTPaintPanel:SetPosition(nID,nLeft,nTop)}

        aAdd(oView:aNotPlayButtonsActions,{"Close",{||oView:oTPaintPanel:ClearAll(),oView:oTPaintPanel:oWnd:End()}})
        aAdd(oView:aNotPlayButtonsActions,{"Load Game",{||RestoreGame(@oController,@bRestoreGameUpdateView)}})
        aAdd(oView:aNotPlayButtonsActions,{"Start of new game",{||FWMsgRun(nil,{||oView:oTPaintPanel:ClearAll(),oController:Reload(),LoadGame(nil,nil,@oController,@oView:oTPaintPanel)},"Wait..","Start of new game")}})
        aAdd(oView:aNotPlayButtonsActions,{"Save a game",{||SaveGame(@oController)}})
        aAdd(oView:aNotPlayButtonsActions,{"About",{||oView:MsgAbout()}})
        aAdd(oView:aNotPlayButtonsActions,{"Top Table",{||oView:ShowTopTable()}})
        aAdd(oView:aNotPlayButtonsActions,{"Exit",{||oView:oTPaintPanel:ClearAll(),oView:oTPaintPanel:oWnd:End()}})

        oTRect:=TRect():New(0,0,365,305)

        cIcon:=oController:GetGame15Data("Game15_Files_ico","ico")
        DEFINE MSDIALOG oView:oDlg TITLE OemToAnsi(cTitle) FROM oTRect:nTop,oTRect:nLeft TO oTRect:nBottom,oTRect:nRight OF GetWndDefault() ICON cIcon PIXEL STYLE WS_POPUP

        jCoordinates:=oController:GetGame15Data("Coordinates")
        jCoordinates["nTop"]:=oTRect:nTop
        jCoordinates["nLeft"]:=oTRect:nLeft
        jCoordinates["nWidth"]:=oTRect:nRight
        jCoordinates["nHeight"]:=oTRect:nBottom

        oView:oTPaintPanel:=TPaintPanel():New(jCoordinates["nTop"],jCoordinates["nLeft"],jCoordinates["nWidth"],jCoordinates["nHeight"],oView:oDlg,.F.)
        oView:oTPaintPanel:Align:=CONTROL_ALIGN_ALLCLIENT

        oView:oTPaintPanel:bRClicked:={|x,y|rClickGame15(@x,@y,@oView,@oController)}
        oView:oTPaintPanel:blClicked:={|x,y|lClickGame15(@x,@y,@oView,@oController)}
        oView:oTPaintPanel:blDBLClick:=oView:oTPaintPanel:blClicked

        FreeObj(@oTRect)

        oView:bInit:={||LoadGame(nil,nil,@oController,@oView:oTPaintPanel)}
        oView:bValid:={||.T.}

    end sequence

return(nil)

method IsActive() class Game15View
return(self:lActive)

method CanExecute() class Game15View
    self:lExecute:=FWIsInCallStack("GAME15.U_GAME15RUN")
    if (self:lExecute).and.(ValType(self:oController)=="O")
        self:lExecute:=(GetClassName(self:oController)=="GAME15.GAME15CONTROLLER")
    endif
return(self:lExecute)

method SaveTopTable() class Game15View

    local cG15Name as character

    local cElapTime as character
    local cStartTime as character

    local dStartDate as date

    local oView:=self as object
    local oController:=self:oController as object

    local oDlg as object
    local oBtn as object
    local oFont as object

    begin sequence

        if (!oView:CanExecute())
            break
        endif

        cStartTime:=oController:GetGame15Data("Game15_Time","cStartTime")
        dStartDate:=oController:GetGame15Data("Game15_Time","dStartDate")
        cStartTime:=oController:Time2NextDay(@cStartTime,@dStartDate)[1]
        cElapTime:=ElapTime(cStartTime,Time())

        cG15Name:=Space(oController:GetPlayerNameSize())

        DEFINE FONT oFont NAME "Arial" SIZE 0,-15 BOLD
        DEFINE MSDIALOG oDlg TITLE OemToAnsi("You Win! Elapsed Time:"+cElapTime+". Enter your name.") FROM 0,0 TO 040,405 OF oView:oDlg PIXEL
            @ 05,02 GET oGet VAR cG15Name PICTURE "@!" OF oDlg SIZE 170,10 PIXEL FONT oFont
            DEFINE SBUTTON oBtn FROM 05,175 TYPE 1 ACTION oDlg:End() OF oDlg ENABLE
        ACTIVATE DIALOG oDlg CENTERED

        FreeObj(@oGet)
        FreeObj(@oBtn)
        FreeObj(@oDlg)
        FreeObj(@oFont)

        DelClassIntf()

        if (Empty(cG15Name))
            cG15Name:="NONAME"
        endif

        oController:SetGame15Player(cG15Name)
        oController:SaveTopTable()

    end sequence

return

method ShowTopTable() class Game15View

    local aListBox as array

    local bInit as codeblock
    local bValid as codeblock

    local cIcon as character

    local jCoordinates as json

    local oView:=self as object
    local oController:=self:oController as object

    local oDlg as object
    local oTRect as object
    local oTWBrowse as object
    local oTPaintPanel as object

    begin sequence

        if (!self:CanExecute())
            break
        endif

        oTRect:=TRect():New(0,0,365,305)

        aListBox:=Array(0)
        oController:BuildLBoxArray(@aListBox)

        cIcon:=oController:GetGame15Data("Game15_Files_ico","ico")
        DEFINE MSDIALOG oDlg TITLE OemToAnsi("TOP Results") FROM oTRect:nTop,oTRect:nLeft TO oTRect:nBottom,oTRect:nRight OF oView:oDlg ICON cIcon PIXEL STYLE WS_POPUP

            jCoordinates:=oController:GetGame15Data("Coordinates")
            jCoordinates["nTop"]:=oTRect:nTop
            jCoordinates["nLeft"]:=oTRect:nLeft
            jCoordinates["nWidth"]:=oTRect:nRight
            jCoordinates["nHeight"]:=oTRect:nBottom

            oTPaintPanel:=TPaintPanel():New(jCoordinates["nTop"],jCoordinates["nLeft"],jCoordinates["nWidth"],jCoordinates["nHeight"],oDlg,.F.)
            oTPaintPanel:Align:=CONTROL_ALIGN_ALLCLIENT

            @ oTRect:nTop+20,oTRect:nLeft+5 LISTBOX oTWBrowse FIELDS HEADER "Time","Result","Name" SIZE oTRect:nRight/2.05,oTRect:nBottom/2.35 OF oTPaintPanel PIXEL COLSIZES 15,25,50
            oTWBrowse:SetArray(aListBox)
            oController:ViewSetListBoxLine(@oTWBrowse,@aListBox)

            oTPaintPanel:bRClicked:={|x,y|rClickTopTable(@x,@y,@oView,@oTPaintPanel,@oTWBrowse,@aListBox)}
            oTPaintPanel:blClicked:={|x,y|lClickTopTable(@x,@y,@oView,@oTPaintPanel,@oTWBrowse,@aListBox)}
            oTPaintPanel:blDBLClick:=oTPaintPanel:blClicked

            FreeObj(@oTRect)

            bInit:={||LoadTopTable(@oTPaintPanel,@oController)}
            bValid:={||.T.}

        ACTIVATE DIALOG oDlg CENTERED ON INIT Eval(bInit) VALID Eval(bValid)

        FWFreeArray(@aListBox)

        FreeObj(oTWBrowse)
        FreeObj(@oTPaintPanel)
        FreeObj(@oDlg)
        DelClassIntf()

    end sequence

return

method ClearTopTable() class Game15View

    local cG15Name as character

    local lClear:=.F. as logical

    local oView:=self as object
    local oController:=self:oController as object

    local oDlg as object
    local oBtn as object
    local oFont as object

    begin sequence

        cG15Name:=Space(oController:GetPlayerNameSize())

        DEFINE FONT oFont NAME "Arial" SIZE 0,-15 BOLD
        DEFINE MSDIALOG oDlg TITLE OemToAnsi("Enter your name.") FROM 0,0 TO 040,405 OF oView:oDlg PIXEL
            @ 05,02 GET oGet VAR cG15Name PICTURE "@!" OF oDlg SIZE 170,10 PIXEL FONT oFont
            DEFINE SBUTTON oBtn FROM 05,175 TYPE 1 ACTION oDlg:End() OF oDlg ENABLE
        ACTIVATE DIALOG oDlg CENTERED

        FreeObj(@oGet)
        FreeObj(@oBtn)
        FreeObj(@oDlg)
        FreeObj(@oFont)

        DelClassIntf()

        if (Empty(cG15Name))
            cG15Name:="NONAME"
        endif

        oController:SetGame15Data("Game15_Player","cGame15Player",cG15Name)
        lClear:=oController:ClearTopTable()

    end sequence

return(lClear)

static procedure LoadGame(x as numeric,y as numeric,oController as object,oTPaintPanel as object)

    local cShape:="Game15_Shapes" as character
    local cProperties as character

    local lCanMove:=.F. as logical
    local lCanDeform:=.F. as logical

    local nID
    local nShape
    local nShapes

    SYMBOL_UNUSED(x)
    SYMBOL_UNUSED(y)

    oTPaintPanel:ClearAll()

    nShapes:=oController:ShapeGetSize(cShape)
    for nShape:=1 to nShapes
        nID:=oController:ShapeGetATValue(cShape,nShape,SHAPE_ID)
        cProperties:=oController:ShapeGetATValue(cShape,nShape,SHAPE_PROPERTIES)
        oTPaintPanel:AddShape(cProperties)
        oTPaintPanel:SetCanMove(nID,lCanMove)
        oTPaintPanel:SetCanDeform(nID,lCanDeform)
    next nShape

return

static function rClickGame15(x as numeric,y as numeric,oTPaintPanel as object,oController as object) as logical
    SYMBOL_UNUSED(x)
    SYMBOL_UNUSED(y)
    SYMBOL_UNUSED(oTPaintPanel)
    SYMBOL_UNUSED(oController)
return(.T.)

static function lClickGame15(x as numeric,y as numeric,oView as object,oController as object) as logical

    local bUpdateView as codeblock
    local bShapeAction as codeblock

    local cShape:="Game15_Shapes" as character
    local cShapeToolTip as character
    local cShapeProperties as character

    local lAction as logical

    local nShape as numeric
    local nShapeAT as numeric
    local nATNoPlayButton as numeric

    SYMBOL_UNUSED(x)
    SYMBOL_UNUSED(y)

    begin sequence

        nShape:=oView:oTPaintPanel:ShapeAtu
        oController:SetGame15Data(cShape,"nShapeAtu",nShape)

        nShapeAT:=oController:ShapeGetIDAT(cShape,nShape)
        lAction:=(nShapeAT>0)

        if (!lAction)
            break
        endif

        bShapeAction:=oController:ShapeGetATValue(cShape,nShapeAT,SHAPE_ACTION)
        lAction:=(ValType(bShapeAction)=="B")

        if (!lAction)
            break
        endif

        cShapeToolTip:=oController:ShapeGetATValue(cShape,nShapeAT,SHAPE_TOOLTIP)
        nATNoPlayButton:=aScan(oView:aNotPlayButtonsActions,{|e as array|e[1]==cShapeToolTip})
        if (nATNoPlayButton>0)
            Eval(oView:aNotPlayButtonsActions[nATNoPlayButton][2])
            break
        endif

        cShapeProperties:=oController:ShapeGetATValue(cShape,nShapeAT,SHAPE_PROPERTIES)
        lICanMove:=ICanMove(x,y,cShapeToolTip,cShapeProperties)
        if (!lICanMove)
            break
        endif

        bUpdateView:={;
            |;
                 nIDPress as numeric;
                ,nLeftPress as numeric;
                ,nTopPress as numeric;
                ,nIDFree as numeric;
                ,nLeftFree as numeric;
                ,nTopFree as numeric;
            |;
                oView:oTPaintPanel:SetPosition(nIDPress,nLeftPress,nTopPress);
               ,oView:oTPaintPanel:SetPosition(nIDFree,nLeftFree,nTopFree);
               ,Tone(3000,1);
        }
        oController:SetGame15Data("Game15_Shapes","bUpdateView",bUpdateView)

        Eval(bShapeAction,{||if(oController:SwapButtons(),oView:SaveTopTable(),nil)})

    end sequence

return(lAction)

static function ICanMove(x as numeric,y as numeric,cShapeToolTip as character,cShapeProperties as character) as logical
    local cToolTip:=Left(cShapeToolTip,3)
    local lICanMove:=("can-move=1"$cShapeProperties) as logical
    SYMBOL_UNUSED(x)
    SYMBOL_UNUSED(y)
    lICanMove:=((lICanMove).and.(cToolTip>="b01".and.cToolTip<="b15"))
return(lICanMove)

static function rClickTopTable(x as numeric,y as numeric,oView as object,oTPaintPanel as object,oTWBrowse as object,aListBox as array) as logical
    SYMBOL_UNUSED(x)
    SYMBOL_UNUSED(y)
    SYMBOL_UNUSED(oView)
    SYMBOL_UNUSED(oTPaintPanel)
    SYMBOL_UNUSED(oTWBrowse)
    SYMBOL_UNUSED(aListBox)
return(.T.)

static function lClickTopTable(x as numeric,y as numeric,oView as object,oTPaintPanel as object,oTWBrowse as object,aListBox as array) as logical

    local aTopTableAction as array

    local cShape:="Game15_TopTable" as character
    local cShapeToolTip as character

    local lAction as logical

    local nShape as numeric
    local nShapeAT as numeric
    local nATTopTableAction as numeric

    local oController:=oView:oController as object

    SYMBOL_UNUSED(x)
    SYMBOL_UNUSED(y)

    begin sequence

        nShape:=oTPaintPanel:ShapeAtu
        nShapeAT:=oController:ShapeGetIDAT(cShape,nShape)
        lAction:=(nShapeAT>0)

        if (!lAction)
            break
        endif

        aTopTableAction:=Array(0)
        aAdd(aTopTableAction,{"Close",{||oTPaintPanel:ClearAll(),oTPaintPanel:oWnd:End()}})
        aAdd(aTopTableAction,{"OK",{||oTPaintPanel:ClearAll(),oTPaintPanel:oWnd:End()}})
        aAdd(aTopTableAction,{"Clear Top Table",{||oView:ClearTopTable(),oController:BuildLBoxArray(@aListBox),oTWBrowse:Refresh()}})

        cShapeToolTip:=oController:ShapeGetATValue(cShape,nShapeAT,SHAPE_TOOLTIP)
        nATTopTableAction:=aScan(aTopTableAction,{|e as array|e[1]==cShapeToolTip})
        if (nATTopTableAction>0)
            Eval(aTopTableAction[nATTopTableAction][2])
        endif

        FWFreeArray(@aTopTableAction)

    end sequence

return(lAction)

static procedure LoadTopTable(oTPaintPanel as object,oController as object)

    local cShape:="Game15_TopTable" as character
    local cProperties as character

    local lCanMove:=.F. as logical
    local lCanDeform:=.F. as logical

    local nID as numeric
    local nShape as numeric
    local nShapes as numeric

    oTPaintPanel:ClearAll()

    nShapes:=oController:ShapeGetSize(cShape)
    for nShape:=1 to nShapes
        nID:=oController:ShapeGetATValue(cShape,nShape,SHAPE_ID)
        cProperties:=oController:ShapeGetATValue(cShape,nShape,SHAPE_PROPERTIES)
        oTPaintPanel:AddShape(cProperties)
        oTPaintPanel:SetCanMove(nID,lCanMove)
        oTPaintPanel:SetCanDeform(nID,lCanDeform)
    next nShape

return

method MsgAbout() class Game15View

    local bInit as codeblock
    local bValid as codeblock

    local cIcon as character

    local jCoordinates as json

    local oView:=self as object
    local oController:=self:oController as object

    local oDlg as object
    local oTRect as object
    local oTPaintPanel as object

    begin sequence

        if (!self:CanExecute())
            break
        endif

        oTRect:=TRect():New(0,0,365,305)

        cIcon:=oController:GetGame15Data("Game15_About","ico")
        DEFINE MSDIALOG oDlg TITLE OemToAnsi(GAME15_PROGRAM+" About") FROM oTRect:nTop,oTRect:nLeft TO oTRect:nBottom,oTRect:nRight OF oView:oDlg ICON cIcon PIXEL STYLE WS_POPUP

            jCoordinates:=oController:GetGame15Data("Coordinates")
            jCoordinates["nTop"]:=oTRect:nTop
            jCoordinates["nLeft"]:=oTRect:nLeft
            jCoordinates["nWidth"]:=oTRect:nRight
            jCoordinates["nHeight"]:=oTRect:nBottom

            oTPaintPanel:=TPaintPanel():New(jCoordinates["nTop"],jCoordinates["nLeft"],jCoordinates["nWidth"],jCoordinates["nHeight"],oDlg,.F.)
            oTPaintPanel:Align:=CONTROL_ALIGN_ALLCLIENT

            LoadMsgAbout(@oTPaintPanel,oController)

            oTPaintPanel:bRClicked:={|x,y|rClickMsgAbout(@x,@y,@oTPaintPanel,@oController)}
            oTPaintPanel:blClicked:={|x,y|lClickMsgAbout(@x,@y,@oTPaintPanel,@oController)}
            oTPaintPanel:blDBLClick:=oTPaintPanel:blClicked

            FreeObj(@oTRect)

            bInit:={||.T.}
            bValid:={||.T.}

        ACTIVATE DIALOG oDlg CENTERED ON INIT Eval(bInit) VALID Eval(bValid)

        FreeObj(@oTPaintPanel)
        FreeObj(@oDlg)
        DelClassIntf()

    end sequence

return

static procedure LoadMsgAbout(oTPaintPanel as object,oController as object)

    local cShape:="Game15_About" as character
    local cProperties as character

    local lCanMove:=.F. as logical
    local lCanDeform:=.F. as logical

    local nID as numeric
    local nShape as numeric
    local nShapes as numeric

    oTPaintPanel:ClearAll()

    nShapes:=oController:ShapeGetSize(cShape)
    for nShape:=1 to nShapes
        nID:=oController:ShapeGetATValue(cShape,nShape,SHAPE_ID)
        cProperties:=oController:ShapeGetATValue(cShape,nShape,SHAPE_PROPERTIES)
        oTPaintPanel:AddShape(cProperties)
        oTPaintPanel:SetCanMove(nID,lCanMove)
        oTPaintPanel:SetCanDeform(nID,lCanDeform)
    next nShape

return

static function rClickMsgAbout(x as numeric,y as numeric,oTPaintPanel as object,oController as object) as logical
    SYMBOL_UNUSED(x)
    SYMBOL_UNUSED(y)
    SYMBOL_UNUSED(oTPaintPanel)
    SYMBOL_UNUSED(oController)
return(.T.)

static function lClickMsgAbout(x as numeric,y as numeric,oTPaintPanel as object,oController as object) as logical

    local aMsgAboutAction as array

    local cShape:="Game15_About" as character
    local cShapeToolTip as character

    local lAction as logical

    local nShape as numeric
    local nShapeAT as numeric
    local nATMsgAboutAction as numeric

    SYMBOL_UNUSED(x)
    SYMBOL_UNUSED(y)

    begin sequence

        nShape:=oTPaintPanel:ShapeAtu
        nShapeAT:=oController:ShapeGetIDAT(cShape,nShape)
        lAction:=(nShapeAT>0)

        if (!lAction)
            break
        endif

        aMsgAboutAction:=Array(0)
        aAdd(aMsgAboutAction,{"Close",{||oTPaintPanel:ClearAll(),oTPaintPanel:oWnd:End()}})
        aAdd(aMsgAboutAction,{PadC("BlackTDN::ADVPL Games ~ GAME 15",40),{||ShellExecute("open",GAME15_URL_BTDNGAME15,"","",1)}})
        aAdd(aMsgAboutAction,{PadC(GAME15_URL_COPYRIGHT,40),{||ShellExecute("open",GAME15_URL_COPYRIGHT,"","",1)}})
        aAdd(aMsgAboutAction,{"OK",{||oTPaintPanel:ClearAll(),oTPaintPanel:oWnd:End()}})

        cShapeToolTip:=oController:ShapeGetATValue(cShape,nShapeAT,SHAPE_TOOLTIP)
        nATMsgAboutAction:=aScan(aMsgAboutAction,{|e as array|e[1]==cShapeToolTip})
        if (nATMsgAboutAction>0)
            Eval(aMsgAboutAction[nATMsgAboutAction][2])
        endif

        FWFreeArray(@aMsgAboutAction)

    end sequence

return(lAction)

static procedure SaveGame(oController as object)

    local cG15File as character
    local cMGFile as character

    local ncGFile as numeric

    begin sequence

        ncGFile:=nOr(GETF_LOCALFLOPPY,GETF_LOCALHARD,GETF_NETWORKDRIVE,GETF_SHAREAWARE)
        cMGFile:="G15FileSave (g15_*.sav)|g15_*.sav"
        cG15File:=cGetFile(cMGFile,"Save Game",NIL,GetTempPath(),.T.,ncGFile,.T.,.T.)

        if (Empty(cG15File))
            break
        endif

        if (oController:SaveGame(@cG15File))
            ApMsgInfo("Arquivo ["+cG15File+".sav] salvo com sucesso",ProcName())
        else
            ApMsgInfo("Ocorreram problemas para salvar o Arquivo ["+cG15File+".sav]",ProcName())
        endif

    end sequence

return

static procedure RestoreGame(oController as object,bUpdateView as codeblock)

    local cG15File as character
    local cMGFile as character

    local ncGFile as character

    begin sequence

        ncGFile:=nOr(GETF_LOCALFLOPPY,GETF_LOCALHARD,GETF_NETWORKDRIVE,GETF_SHAREAWARE)
        cMGFile:="G15FileSave (g15_*.sav)|g15_*.sav"
        cG15File:=cGetFile(cMGFile,"Restore Game",NIL,GetTempPath(),.F.,ncGFile,.T.,.T.)

        if (Empty(cG15File))
            break
        endif

        if (oController:RestoreGame(@cG15File,@bUpdateView))
            ApMsgInfo("Arquivo ["+cG15File+"] restaurado com sucesso",ProcName())
            //Reinicia a Contagem de Tempo apos o Reload
            oController:StartTime()
        else
            ApMsgInfo("Ocorreram problemas para restaurar o Arquivo ["+cG15File+"]",ProcName())
        endif

    end sequence

return
