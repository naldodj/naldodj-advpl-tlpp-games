#include "totvs.ch"
#include "parmtype.ch"
#include "tlpp-core.th"

/*
                 _         _
 ___  _   _   __| |  ___  | | __ _   _
/ __|| | | | / _` | / _ \ | |/ /| | | |
\__ \| |_| || (_| || (_) ||   < | |_| |
|___/ \__,_| \__,_| \___/ |_|\_\ \__,_|

    Class:Sudoku
    Autor:Marinaldo de Jesus [http://www.blacktdn.com.br]
    Data:22/05/2025
    Uso:Jogo Sudoku

    * Copyright 2012-3999 marinaldo.jesus <http://www.blacktdn.com.br>

*/

namespace sudoku
using namespace sudoku
using namespace dna.tech
using namespace naldodj.games

function u_SudokuRun()
return(sudoku.Sudoku():Run())

procedure u_SudokuRunTests(nTests as numeric,lHTMLTable as logical,lThemeIsDark as logical)

    local cSudokuHTML as character
    local cSudokuPath as character
    local cSudokuFile as character

    local nTest as numeric

    DEFAULT nTests:=1
    DEFAULT lHTMLTable:=.T.
    DEFAULT lThemeIsDark:=WebApp():ThemeIsDark()

    cSudokuPath:=GetTempPath()

    cSudokuHTML:=""
    for nTest:=1 to nTests
        cSudokuHTML:=sudoku.Sudoku():RunTest(lHTMLTable,lThemeIsDark)
        cSudokuFile:="naldodj.games.sudoku."+DToS(Date())+"."+StrTran(Time(),":","-")+"."+StrZero(Randomize(0,9999),4)+".html"
        if (MemoWrite(cSudokuPath+cSudokuFile,cSudokuHTML))
            ShellExecute("open",cSudokuPath+cSudokuFile,"","",1)
        endif
    next nTest

return

procedure u_SudokuMCPRunServer()
    local lTMCPServer as logical
    local oTMCPServer as object
    lTMCPServer:=FindClass("DNA.TECH.TMCPServer")
    if (lTMCPServer)
        oTMCPServer:=TMCPServer():New("SudokuMCPServer")
        oTMCPServer:SetMemberData("cFileMsgIN","c:\tmp\hb.totvs.mcp.msg.json")
        oTMCPServer:SetMemberData("cFileMsgOUT","c:\tmp\totvs.hb.mcp.msg.json")
        oTMCPServer:SetMemberData("cFileMCPServerStop","sudokumcpserver.stop")
        oTMCPServer:AddTool("open_sudoku","Opens the Sudoku game in the browser.",{|jParam|MCPExecute(jParam,"SUDOKU.U_SUDOKURUN")})
        oTMCPServer:AddTool("open_sudoku_test","Runs a Sudoku test and opens the result in the browser.",{|jParam|MCPExecute(jParam,"SUDOKU.U_SUDOKURUNTESTS")})
        oTMCPServer:Run()
        MsObjects():FreeObjects(oTMCPServer)
    endif
return

class Sudoku
   static method Run(lSudokuKeepLevelOption as logical,nSudokuLastLevel as numeric) as logical
   static method RunTest(lHTMLTable as logical,lThemeIsDark as logical) as character
end class

static method Run(lSudokuKeepLevelOption,nSudokuLastLevel) class Sudoku

    local lRun as logical
    local lSudokuNewGame as logical

    local oController as object

    paramtype 1 var lSudokuKeepLevelOption as logical optional default .F.
    paramtype 2 var nSudokuLastLevel as numeric optional default 0

    oController:=SudokuController():New()

    lRun:=oController:CanExecute()

    if (lRun)
        if (lSudokuKeepLevelOption)
            oController:SudokuKeepLevelOption(lSudokuKeepLevelOption)
            oController:SudokuLastLevel(nSudokuLastLevel)
        endif
        lRun:=oController:Activate((!lSudokuKeepLevelOption),.T.)
        if (lRun)
            lSudokuNewGame:=oController:SudokuDataGet("lSudokuNewGame",.F.)
            lSudokuKeepLevelOption:=oController:SudokuKeepLevelOption()
            if (lSudokuKeepLevelOption)
                nSudokuLastLevel:=oController:SudokuLastLevel()
            endif
            oController:DeActivate()
            if (lSudokuNewGame)
                MsObjects():FreeObjects(@oController)
                lRun:=sudoku.Sudoku():Run(lSudokuKeepLevelOption,nSudokuLastLevel)
            endif
        endif
    endif

    MsObjects():FreeObjects(@oController)

return(lRun)

static method RunTest(lHTMLTable,lThemeIsDark) class Sudoku

    local cSudokuBoard as character

    local lRun as logical

    local oController as object

    paramtype 1 var lHTMLTable as logical optional default .T.
    paramtype 2 var lThemeIsDark as logical optional default WebApp():ThemeIsDark()

    oController:=SudokuController():New()

    lRun:=oController:CanExecute()

    if (lRun)
        cSudokuBoard:=oController:RunTest(lHTMLTable,lThemeIsDark)
    endif

    MsObjects():FreeObjects(@oController)

return(cSudokuBoard)

static function MCPExecute(jParam,cFunction) as numeric
    local nRet as numeric
    nRet:=WinExec("C:\totvs\protheus1212410\web.agent.sudoku.mcp.client.bat "+cFunction)
return(nRet)
